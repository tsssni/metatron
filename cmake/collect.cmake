function(unite units)
    get_property(metatron-units TARGET metatron-build PROPERTY metatron-units)
    list(APPEND metatron-units ${units})
    set_property(TARGET metatron-build PROPERTY metatron-units ${metatron-units})
endfunction()

function(extend ext)
    find_package(${ext} REQUIRED)
    get_property(metatron-exts TARGET metatron-build PROPERTY metatron-exts)
    list(APPEND metatron-exts ${ext})
    set_property(TARGET metatron-build PROPERTY metatron-exts ${metatron-exts})
endfunction()

function(modulo path)
    list(APPEND CMAKE_MODULE_PATH ${path})
    set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} PARENT_SCOPE)
    file(GLOB mods ${path}/Find*.cmake)

    get_property(metatron-mods TARGET metatron-build PROPERTY metatron-mods)
    list(APPEND metatron-mods ${mods})
    set_property(TARGET metatron-build PROPERTY metatron-mods ${metatron-mods})
endfunction()

function(evaluate unit path mode)
    get_property(system TARGET metatron-build PROPERTY metatron-system)
    get_property(arch TARGET metatron-build PROPERTY metatron-arch)
    set(target metatron-${unit})
    set(setup ${path}/setup.cmake)
    set(deps)
    set(access)
    set(sources)
    set(include)

    # collect sources
    if(${mode} STREQUAL "ext")
        if (EXISTS ${path}/src)
            message(STATUS "build independent ${target}")
            add_library(${target} SHARED)
            set(mode "lib")
            set(access PUBLIC)
        else()
            message(STATUS "build external ${target}")
            add_library(${target} INTERFACE)
            set(access INTERFACE)
        endif()
        include(${setup})
    else()
        file(GLOB library LIST_DIRECTORIES true "${path}/source*")
        file(GLOB include LIST_DIRECTORIES true "${path}/include")

        if(library)
            if(${mode} STREQUAL "bin")
                message(STATUS "build binary ${target}")
                add_executable(${target})
            else()
                message(STATUS "build library ${target}")
                add_library(${target} SHARED EXCLUDE_FROM_ALL)
            endif()
            set(access PUBLIC)

            list(APPEND features " ")
            list(APPEND deps core)
            include(${setup})
            foreach(feature ${features})
                string(STRIP "${feature}" feature)
                string(REGEX REPLACE "^(.+)$" "-\\1" feature "${feature}")
                file(GLOB_RECURSE files ${path}/source${feature}/*.cpp)
                list(APPEND sources ${files})
            endforeach()
        elseif(${mode} STREQUAL "src")
            add_library(${target} INTERFACE)
            set(access INTERFACE)
            if(include)
                message(STATUS "build headeronly ${target}")
                set(mode "inc")
            else()
                message(STATUS "build interface ${target}")
                set(mode "inf")
            endif()
            include(${setup})
        else()
            message(FATAL_ERROR "sources not found")
        endif()
    endif()

    if(sources)
        target_sources(${target} PRIVATE ${sources})
    endif()
    if(include)
        target_include_directories(
            ${target} ${access}
            $<BUILD_INTERFACE:${path}/include>
            $<INSTALL_INTERFACE:include/${target}>
        )
    endif()

    set_property(TARGET ${target} PROPERTY metatron-deps ${deps})
    set_property(TARGET ${target} PROPERTY metatron-path ${path})
    set_property(TARGET ${target} PROPERTY metatron-mode ${mode})
    set_property(TARGET ${target} PROPERTY metatron-access ${access})
endfunction()

function(solve path mode)
    message(STATUS "processing ${mode}...")
    if (IS_DIRECTORY ${path})
        file(GLOB units RELATIVE ${path} ${path}/*)
        set(build-units)

        foreach(unit ${units})
            if(IS_DIRECTORY ${path}/${unit})
                list(APPEND build-units ${unit})
                evaluate(${unit} ${path}/${unit} ${mode})
            endif()
        endforeach()
        
        unite("${build-units}")
    endif()
endfunction()

function(collect)
    solve(${CMAKE_CURRENT_LIST_DIR}/ext "ext")
    solve(${CMAKE_CURRENT_LIST_DIR}/src "src")
    solve(${CMAKE_CURRENT_LIST_DIR}/bin "bin")
endfunction()
